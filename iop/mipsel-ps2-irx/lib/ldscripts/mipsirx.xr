/* Script for -r */
/* Link script for PlayStation 2 IRXs
 * Written by Douglas C. Knight <fsdck@uaf.edu>
 */
OUTPUT_FORMAT("elf32-littlemips")
ENTRY(_start)
SECTIONS
{
  .text : {
    CREATE_OBJECT_SYMBOLS
    * ( .text )
    * ( .text.* )
    * ( .init )
    * ( .fini )
  } = 0
  .rodata : {
    * ( .rdata )
    * ( .rodata )
    * ( .rodata1 )
    * ( .rodata.* )
  } = 0
  .data : {
    * ( .data )
    * ( .data1 )
    * ( .data.* )
  }
  .sdata : {
    * ( .lit8 )
    * ( .lit4 )
    * ( .sdata )
    * ( .sdata.* )
  }
  .sbss : {
    * ( .sbss )
    * ( .scommon )
  }
  .bss : {
    * ( .bss )
    * ( COMMON )
  }
  /* These are the stuff that we don't want to be put in an IRX.  */
  /DISCARD/ : {
	* ( .MIPS.abiflags )
	* ( .gnu.attributes )
	* ( .comment )
	* ( .reginfo )
	* ( .mdebug.* )
	/* This must go because it confuses the IOP kernel (treated as a reloc section). */
	* ( .pdr )
	/* Until I can figure out if there's a better way to rid ourselves of .rel.dyn
	   this will have to do.  - MRB  */
	* ( .rel.dyn )
  }
}
